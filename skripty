01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________

05 suma srazek mesic 6 = 
-- Suma srážek v daném roce v měsíci číslo 6
SUMX(FILTER(data,data[mesic] = 6),data[srazky])

________________________________________________

06 suma srazek mosnov = 
-- suma srazek v danem roce v lokalita MOSNOV
SUMX(FILTER(data,data[lokalita] = "MOSNOV"),data[srazky])

____________________________________________________

07 minimalni datum v roce = MIN(data[datum])


_____________________________________________________________

08 pocet vyskytu teplota nad 30 = 
-- pocet hodnot v danem ze sloupce datum, kdy maximalni_teplota > 30
COUNTX(
    FILTER(data,data[maximalni_teplota] >30),
data[datum])

_________________________________________________________

09 maximalni teplota v danem roce = 
-- maximum ze sloupce maximalni_teplota v danem roce
MAX(data[maximalni_teplota])

________________________________________________

10 mesto nejvyssi teploty ve sledovanem obdobi = 
-- 1. maximalni teplota v danem roce
-- 2. tabulku filtrovanou pres lokalitu dale filtrovat na maximalni teplotu
-- 3. hodnoty ze sloupce lokalita tabulky filtrovane na rok
-- a maximalni teplotu
CONCATENATEX(
  FILTER(data,data[maximalni_teplota] = MAX(data[maximalni_teplota])),
    
    --CALCULATETABLE(DISTINCT(data[lokalita])
    --,data[maximalni_teplota] = MAX(data[maximalni_teplota])),
    
    data[lokalita],", ")

___________________________________________________________

11 suma srazek mezirocne = 
-- suma srazek v danem roce
SUM(data[srazky])
-- MÍNUS
-
-- suma srazek v predchozim roce
SUMX(
FILTER(
    ALL(data),data[rok] = MAX(data[rok])-1),
data[srazky])

_______________________________________________________________

12 datum max teploty v roce = 
-- prvni datum maximalni teploty
MINX(
FILTER(data,data[maximalni_teplota] =  MAX(data[maximalni_teplota])),
data[datum])

_______________________________________________________

13 maximalni teplota v roce v lokalitach ruzyne mosnov = 
-- maximalni teplota v danem roce v lokalite ruzyne nebo mosnov
MAXX(
FILTER(data,data[lokalita] = "RUZYNE" || data[lokalita] = "MOSNOV"),
data[maximalni_teplota])
_________________________________________________________

14 maximalni teplota v ruzyni a srazky vetsi 0 = 
-- maximum ze sloupce maximalni_teplota v danem roce
-- v lokalite ruzyne, kdy srazky vetsi 0
MAXX(FILTER(data,data[lokalita] = "RUZYNE" && data[srazky] >0),data[maximalni_teplota])
________________________________________
15 minimalni datum = min(data[datum])
_______________________________________________________

16 minimalni datum v predchozim roce = 
MINX(
 FILTER(ALL(data),data[rok] = min(data[rok])-1),
 data[datum])
_____________________________________________________

17 minimalni datum mesic 12 = 
MINX(FILTER(data,data[mesic] = 12),data[datum])

_____________________________________________________________

18 prvni den maximalni teploty mosnov = 
MINX(
    FILTER(data,
    data[lokalita] = "MOSNOV" &&
    data[maximalni_teplota] = 
MAXX(FILTER(data,data[lokalita] = "MOSNOV"),data[maximalni_teplota])),
data[datum])

____________________________________________________________________

19 cislo tydne prvniho destiveho vikendu = 
MINX(
FILTER(data,data[srazky] > 0 && 
(WEEKDAY(data[datum],2) = 6 || WEEKDAY(data[datum],2)=7)),WEEKNUM(data[datum],2))
______________________________________________________________________________

20 suma srazek za dane a predchozi roky = 
SUMX(
FILTER(ALL(data),data[rok] <= MAX(data[rok])),
data[srazky])


____________________________________________________________________________

21 suma srazek predchozi rok = 
SUMX(
    FILTER(
        ALL(data),
    data[lokalita] = SELECTEDVALUE(data[lokalita])
    && data[rok] = SELECTEDVALUE(data[rok])-1),
data[srazky])
